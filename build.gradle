import kotlinx.kover.gradle.plugin.dsl.MetricType

plugins {
    id "java-library"
    id "signing"
    id 'maven-publish'
    id "org.jetbrains.kotlin.jvm" version "${kotlinVersion}"
    id "io.gitlab.arturbosch.detekt" version "${detektVersion}"
    id "org.jetbrains.kotlinx.kover" version "${koverVersion}"
    id 'nu.studer.credentials' version "${credentialsVersion}"
}

group "com.rapatao.ruleset"
version "0.2.2-SNAPSHOT"

repositories {
    mavenCentral()
}

dependencies {
    implementation("org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}")
    implementation("org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}")

    implementation("org.mozilla:rhino:${rhinoVersion}")

    implementation("com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}")
    implementation("com.fasterxml.jackson.module:jackson-module-kotlin:${jacksonVersion}")

    testImplementation("org.junit.jupiter:junit-jupiter:${junitVersion}")
    testImplementation("org.hamcrest:hamcrest:${hamcrestVersion}")
}

test {
    useJUnitPlatform()
}

java {
    withSourcesJar()
    withJavadocJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

detekt {
    buildUponDefaultConfig = true
    allRules = false
}

koverReport {
    verify {
        rule {
            enabled = true
            bound {
                metric = MetricType.BRANCH
                minValue = 80
            }
            bound {
                metric = MetricType.LINE
                minValue = 90
            }
            bound {
                metric = MetricType.INSTRUCTION
                minValue = 90
            }
        }
    }

    defaults {
        html {
            onCheck = true
        }
    }
}

signing {
    useGpgCmd()
    sign configurations.archives
    sign publishing.publications
}

String ossrhUsername = credentials.forKey('ossrhUsername')
String ossrhPassword = credentials.forKey('ossrhPassword')

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            pom {
                name = "RuleSet Engine"
                description = "A simple but fully customizable rule engine"
                url = "https://github.com/rapatao/ruleset-engine"
                packaging = "jar"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "rapatao"
                        name = "Luiz Henrique Rapatao"
                        email = "rapatao@rapatao.com"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/rapatao/ruleset-engine.git"
                    developerConnection = "scm:git:ssh://github.com/rapatao/ruleset-engine.git"
                    url = "https://github.com/rapatao/ruleset-engine"
                }
            }
        }
    }
    repositories {
        maven {
            url = uri("${buildDir}/publishing-repository")
        }
        maven {
            def releasesRepoUrl = uri("https://oss.sonatype.org/service/local/staging/deploy/maven2/")
            def snapshotsRepoUrl = uri("https://oss.sonatype.org/content/repositories/snapshots/")
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}
